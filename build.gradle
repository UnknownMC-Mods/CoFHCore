buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

if (System.getenv().BUILD_NUMBER)
    version += "." + System.getenv().BUILD_NUMBER
group = "cofh"
archivesBaseName = "cofhcore"

sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    version = "${minecraft_version}-${forge_version}"
    mappings = mcp_mappings
    runDir = "run"
    makeObfSourceJar = false
}

repositories {
    maven { url = "https://maven.covers1624.net/" } // RedstoneFlux
    maven { url = "https://dvs1.progwml6.com/files/maven" } // JEI
}

dependencies {

    deobfProvided "cofh:RedstoneFlux:${redstoneflux_version}:universal"
    deobfProvided "mezz.jei:jei_${jei_version}"
}

processResources {

    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

def commonManifest = {
    attributes "FMLAT": "cofh_at.cfg"
}

jar {
    manifest commonManifest
}

tasks.register("deobfJar", Jar) {
    from sourceSets.main.output
    classifier = "deobf"
    manifest commonManifest
}

tasks.register("srcJar", Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
    manifest commonManifest
}

artifacts {
    archives deobfJar
    archives srcJar
}
